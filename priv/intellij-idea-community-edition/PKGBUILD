# $Id$
# Maintainer: sumito3478 <sumito3478@gmail.com>

# maintainer of the original package
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=priv-intellij-idea-community-edition
pkgver=14.0.2
_pkgver=139.659.2
pkgrel=1
pkgdesc="IDE for Java, Groovy and other programming languages with advanced refactoring features"
arch=('x86_64')
url="http://www.jetbrains.org/"
license=('Apache')
depends=('java-environment' 'giflib')
install=idea.install
source=(http://download.jetbrains.com/idea/ideaIC-$pkgver.tar.gz \
        idea.desktop idea.sh vmoptions.diff)
conflicts=('intellij-idea-community-edition' 'intellij-idea-libs')
provides=("intellij-idea-community-edition=$pkgver" "intellij-idea-libs=$pkgver")

build() {
  cd "$srcdir/idea-IC-$_pkgver/bin"
  patch -p1 < "$srcdir/vmoptions.diff"
}

package() {
  install -d -m755 "$pkgdir/"usr/share
  cp -a "idea-IC-$_pkgver" "$pkgdir"/usr/share/intellijidea-ce

  # rm "$pkgdir"/usr/share/intellijidea-ce/bin/{fsnotifier,libbreakgen}

  # make sure that all files are owned by root
  chown -R root:root "$pkgdir/usr/share"

  # never wait on user input when starting idea
  sed -i '/.*read IGNORE.*/ d' "$pkgdir"/usr/share/intellijidea-ce/bin/idea.sh

  install -D -m755 "$srcdir"/idea.sh "$pkgdir"/usr/bin/idea.sh
  install -D -m644 "$srcdir"/idea.desktop "$pkgdir"/usr/share/applications/idea.desktop
  install -D -m644 "$pkgdir"/usr/share/intellijidea-ce/bin/idea.png \
                   "$pkgdir"/usr/share/pixmaps/idea.png
}

# vim:set ts=2 sw=2 et:
md5sums=('3240c20ed8ce505209876258fd37e663'
         '074bca11876cd1129e74feafd6271670'
         'f27bad35ee8e6445ca2f8a591bca895a'
         'a66c145c99413948fffb66af41630cc2')
