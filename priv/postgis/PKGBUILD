# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

_pkgname=postgis
pkgname=priv-$_pkgname
conflicts=($_pkgname)
provides=($_pkgname)
pkgver=2.1.3
pkgrel=3
pkgdesc="Adds support for geographic objects to PostgreSQL"
arch=('i686' 'x86_64')
url="http://postgis.net/"
license=('GPL')
depends=('priv-postgresql' 'priv-gdal' 'json-c')
changelog=$_pkgname.changelog
source=(http://download.osgeo.org/postgis/source/${_pkgname}-${pkgver}.tar.gz
        fix_json_012.patch)
sha256sums=('c17812aa4bb86ed561dfc65cb42ab45176b94e0620de183a4bbd773d6d876ec1'
            '68fd36730baf96341744c03582b0e8af309328ba79bb59fa981a515299cc1bda')

prepare() {
  cd ${_pkgname}-${pkgver}

  # Compilation failure from upstream. FS#40528
  patch -p1 < "$srcdir"/fix_json_012.patch 
}

build() {
  cd ${_pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${_pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
